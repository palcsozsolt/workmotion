plugins {
	id 'java'
	id 'org.springframework.boot' version '2.7.5'
	id 'io.spring.dependency-management' version '1.0.15.RELEASE'
	id 'org.openapi.generator' version '5.3.0'
}

group = 'com.homework'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
	mavenCentral()
}

compileJava.dependsOn('openApiGenerate')

openApiValidate {
    inputSpec = "$projectDir/openapi_yaml/api.yaml".toString()
}

openApiGenerate {
    generatorName = "spring"
    library = "spring-boot"
    inputSpec = "$projectDir/openapi_yaml/api.yaml".toString()
    outputDir = "$projectDir".toString()
    globalProperties = [
        modelDocs      : "false",
        models         : "",
        apis           : "",
        supportingFiles: "false"
    ]
    configOptions = [
            useOptional          : "true",
            performBeanValidation: "false",
            useBeanValidation    : "true",
            useTags              : "true",
            singleContentTypes   : "true",
            title                : projectDir.name,
            java8                : "false",
            serializableModel    : "true",
            artifactId           : projectDir.name,
            interfaceOnly        : "true",
            apiPackage           : "com.homework.workmotion.generated.api",
            modelPackage         : "com.homework.workmotion.generated.model",
            invokerPackage       : "com.homework.workmotion.generated"
    ]
}

ext {
	springStateMachineVersion = '3.2.0'
	swaggerVersion = '3.0.0'
	jacksonDatabindVersion = '0.2.4'
	mySqlConnectorVersion = '8.0.31'
	cucumberVersion = '6.9.0'
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation "org.springframework.statemachine:spring-statemachine-core:$springStateMachineVersion"
    implementation "io.springfox:springfox-swagger-ui:$swaggerVersion"
    implementation "io.springfox:springfox-swagger2:$swaggerVersion"
    implementation "org.openapitools:jackson-databind-nullable:$jacksonDatabindVersion"
	implementation "com.mysql:mysql-connector-j:$mySqlConnectorVersion"
	
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	
	testImplementation ('org.springframework.boot:spring-boot-starter-test') {
       exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
       exclude group: 'org.junit.vintage', module: 'junit-jupiter'
       exclude group: 'org.junit.vintage', module: 'junit-jupiter-api'
    }
	testImplementation "io.cucumber:cucumber-core:$cucumberVersion"
	testImplementation "io.cucumber:cucumber-java:$cucumberVersion"
	testImplementation "io.cucumber:cucumber-junit:$cucumberVersion"
	testImplementation "io.cucumber:cucumber-spring:$cucumberVersion"
	testImplementation "io.cucumber:cucumber-junit-platform-engine:$cucumberVersion"
	testImplementation 'com.h2database:h2'
}

tasks.named('test') {
	useJUnitPlatform()
}
